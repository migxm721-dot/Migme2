buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.0'
    }
}

apply plugin: 'com.android.application'

description = 'migme android client'

repositories {
    google()
    mavenCentral()
    maven {
        url 'https://tools.projectgoth.com/nexus/content/groups/public/'
        credentials {
            username System.getenv("NEXUS_USERNAME")
            password System.getenv("NEXUS_PASSWORD")
        }
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.projectgoth:common-lib:0.0.5'
    implementation 'com.projectgoth.localization:localization-lib:0.0.4'
    //Caution: checking the project exist or not is for jenkins
    //         for development, recommend to sync the 4 dependencies below to local
    //         sync paritially will have MultipleDex exception at build time
    if (file('../client-miniblog/build.gradle').exists()) {
        println "use local client-miniblog"
        implementation project(':client-miniblog')
    } else {
        println "use pre-build client-miniblog lib 0.0.45"
        implementation 'com.projectgoth.client-miniblog:client-miniblog:0.0.45-SNAPSHOT'
    }
    if (file('../mig33FusionClientService/build.gradle').exists()) {
        println "use local mig33FusionClientService"
        implementation project(':mig33FusionClientService')
    } else {
        println "use pre-build mig33FusionClientService lib 0.0.83"
        implementation 'com.projectgoth.mig33FusionClientService:mig33FusionClientService:0.0.83-SNAPSHOT'
    }
    if (file('../mig33ClientNetworkService/build.gradle').exists()) {
        println "use local mig33ClientNetworkService"
        implementation project(':mig33ClientNetworkService')
    } else {
        println "use pre-build mig33ClientNetworkService lib 0.0.78"
        implementation 'com.projectgoth:mig33ClientNetworkService:0.0.78-SNAPSHOT'
    }
    if (file('../mig33JavaClientFramework/build.gradle').exists()) {
        println "use local mig33JavaClientFramework"
        implementation project(':mig33JavaClientFramework')
    } else {
        println "use pre-build mig33JavaClientFramework lib 0.0.37"
        implementation 'com.projectgoth.diggle:diggle:0.0.37-SNAPSHOT'
    }
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation project(':facebookSDK')
    implementation 'com.fiksu.asotracking:fiksu:4.1.1'
    implementation 'com.google.android.gms:play-services-analytics:21.5.0'
    implementation 'com.samsung.spen:libspen:2.2.5'
    implementation 'com.samsung.spen:libspen_multiwindow:2.2.5'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'com.deezer.sdk:deezer:0.10.16'

    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.1'
    testImplementation 'org.robolectric:robolectric:3.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}

project.ext.set("vasId", getVasId())

android {
    namespace 'com.projectgoth'
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId 'com.projectgoth'
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode 65
        versionName "5.01.016"
        archivesBaseName = "migmeDroid"
    }

    signingConfigs {
        debug {
            storeFile file("appsigning/debug/debug.keystore")
            keyAlias "androiddebugkey"
        }
        release {
            storeFile file("appsigning/keystore_2010_android.jks")
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias "mig33_oct2010"
            keyPassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
        }
    }

    lint {
        //FIXME: need to turn on lint check in the future
        checkReleaseBuilds false
    }

    flavorDimensions += "release"

    productFlavors {
        full {
            dimension "release"
        }
        //special is for specific purpose, for example disable deezer region check
        special {
            dimension "release"
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            String versionName = defaultConfig.versionName
            String suffix = project.vasId == "001" ?
                    "-" + versionName + ".apk" :
                    "-" + versionName + "($project.vasId)" + ".apk"
            outputFileName = outputFileName.replace(".apk", suffix)
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

        androidTest {
            java.srcDir('tests')
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

String getVasId() {
    File configFile = file("assets/app.properties")
    String buildId = ""
    if (configFile.length() > 0) {
        configFile.eachLine { line ->
            if (line.contains("build.id=")) {
                def (name, value) = line.tokenize("=")
                buildId = value
            }
        }
    } else {
        println "cannot open assets/app.properties!!!"
    }

    return buildId.trim()
}
